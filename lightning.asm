lightning
	ld a,0
lning_frame_lp
	inc a
	push af
	halt
	call lning_frame
	pop af
	cp 0x45
	jr nz,lning_frame_lp

	; clear frame content
	halt
	ld hl,0x4081
	call clear8lines
	ld hl,0x40a1
	call clear8lines
	ld hl,0x40c1
	call clear8lines
	ld hl,0x40e1
	call clear8lines
	ld hl,0x4801
	call clear8lines
	ld hl,0x4821
	call clear8lines
	ld hl,0x4841
	call clear8lines
	ld hl,0x4861
	call clear8lines
	ld hl,0x4881
	call clear8lines
	ld hl,0x48a1
	call clear8lines

	; set white on black
	ld hl,0x5881
	ld de,32-14
	ld a,7
	ld b,10
whiteblack_lp
	rept 14
		ld (hl),a
		inc l
	endm
	add hl,de
	djnz whiteblack_lp

	ld b,16
staticframe_lp
	halt
	push bc
	call staticframe
	pop bc
	djnz staticframe_lp
	ret

otr_static_fade
	ld b,11
	ld ix,drawrect_data
statfade_framelp
	push bc
	halt
	call staticframe
	call otr_fade
	call drawrect
	halt
	call staticframe
	call drawrect
	pop bc
	djnz statfade_framelp

	ret

otr_fade
	ld hl,0x5860
	ld de,16
otr_fadeline
	ld b,16
otr_fadechar
	ld a,(hl)
	dec a
	jr z,otr_fadeskip
	ld (hl),a
otr_fadeskip
	inc l
	djnz otr_fadechar
	add hl,de
	ld a,h
	cp 0x5b
	jp nz,otr_fadeline
	ret

oyl_fade
	ld b,12
	ld ix,drawrect_data_2
oylfade_framelp
	push bc
	halt
	call oylstaticframe
	call otr_fade
	call drawrect
	halt
	call oylstaticframe
	call drawrect
	pop bc
	djnz oylfade_framelp

	; clear 6x6 square for a man to run out of.
	ld hl,0x5026
	call clearw6
	ld hl,0x5046
	call clearw6
	ld hl,0x5066
	call clearw6
	ld hl,0x5086
	call clearw6
	ld hl,0x50a6
	call clearw6
	ld hl,0x50c6
	call clearw6

	; white on black
	ld hl,0x5a26
	ld de,32-5
	ld a,7+64
	ld b,6
clearat6lp
	ld (hl),a
	inc l
	ld (hl),a
	inc l
	ld (hl),a
	inc l
	ld (hl),a
	inc l
	ld (hl),a
	inc l
	ld (hl),a
	add hl,de
	djnz clearat6lp

	ret

drawrect_data
	dw 0x5860
	db 16,10 ; outer width, inner height
	dw 0x5860
	db 16,10
	dw 0x5860
	db 15,10
	dw 0x5881
	db 15,10
	dw 0x5881
	db 14,10
	dw 0x5881
	db 14,10
	dw 0x58a1
	db 13,10

	dw 0x58a2
	db 13,10
	dw 0x58a2
	db 12,10
	dw 0x58c2
	db 12,10
	dw 0x58c3
	db 11,10
	dw 0x58c3
	db 11,10
	dw 0x58e3
	db 10,10
	dw 0x58e3
	db 10,10

	dw 0x58e3
	db 10,10
	dw 0x58e3
	db 10,10
	dw 0x58e3
	db 10,10
	dw 0x58e3
	db 10,10
	dw 0x58e3
	db 10,10
	dw 0x58e3
	db 10,10
	dw 0x58e3
	db 10,10
	dw 0x58e3
	db 10,10

drawrect_data_2
	dw 0x58e3
	db 10,10
	dw 0x5903
	db 10,10
	dw 0x5903
	db 10,10
	dw 0x5923
	db 10,9
	dw 0x5943
	db 10,9
	dw 0x5944
	db 9,9
	dw 0x5964
	db 9,8
	dw 0x5984
	db 9,8
	dw 0x5984
	db 9,8
	dw 0x59a4
	db 9,7
	dw 0x59c5
	db 8,7
	dw 0x59c5
	db 8,7
	dw 0x59e5
	db 8,6
	dw 0x5a05
	db 8,6

	dw 0x5a05
	db 8,6
	dw 0x5a05
	db 8,6
	dw 0x5a05
	db 8,6
	dw 0x5a05
	db 8,6
	dw 0x5a05
	db 8,6
	dw 0x5a05
	db 8,6
	dw 0x5a05
	db 8,6
	dw 0x5a05
	db 8,6
	dw 0x5a05
	db 8,6
	dw 0x5a05
	db 8,6


drawrect
	ld l,(ix)
	inc ix
	ld h,(ix)
	inc ix
	ld b,(ix)
	inc ix
	ld d,(ix)
	inc ix

	ld c,b
	dec c
	push bc

	push hl
recttop_lp
	ld (hl),0x06
	inc l
	djnz recttop_lp
	pop hl

	ld b,d
	ld de,0x20
rectsides_lp
	add hl,de
	push hl
	ld (hl),0x06
	ld a,l
	add a,c ; outer width - 1
	ld l,a
	ld (hl),0x06
	pop hl
	djnz rectsides_lp
	add hl,de

	pop bc
rectbot_lp
	ld (hl),0x06
	inc l
	djnz rectbot_lp

	ret

staticframe
static_source
	ld hl,0
	ld de,0x4081
	call static8lines
	ld de,0x40a1
	call static8lines
	ld de,0x40c1
	call static8lines
	ld de,0x40e1
	call static8lines
	ld de,0x4801
	call static8lines
	ld de,0x4821
	call static8lines
	ld de,0x4841
	call static8lines
	ld de,0x4861
	call static8lines
	ld de,0x4881
	call static8lines
	ld de,0x48a1
	call static8lines
	ld a,h
	and 0x1f
	ld h,a
	ld (static_source+1),hl
	ret

oylstaticframe
	ld hl,(static_source+1)
	ld de,0x4804
	call static8x8
	ld de,0x4824
	call static8x8
	ld de,0x4844
	call static8x8
	ld de,0x4864
	call static8x8
	ld de,0x4884
	call static8x8
	ld de,0x48a4
	call static8x8
	ld de,0x48c4
	call static8x8
	ld de,0x48e4
	call static8x8
	ld de,0x5004
	call static8x8
	ld de,0x5024
	call static8x8
	ld a,h
	and 0x1f
	ld h,a
	ld (static_source+1),hl
	ret

static8lines
	ld b,8
	xor a
static8lines_lp
	push bc
	push de
	rept 14
		ldi
	endm
	pop de
	inc d
	pop bc
	djnz static8lines_lp
	ret

static8x8
	ld b,8
	xor a
static8x8_lp
	push bc
	push de
	rept 8
		ldi
	endm
	pop de
	inc d
	pop bc
	djnz static8x8_lp
	ret

clear8lines
	ld b,8
	xor a
clear8lines_lp
	push hl
	rept 14
		ld (hl),a
		inc l
	endm
	pop hl
	inc h
	djnz clear8lines_lp
	ret

clearw6
	ld b,8
	xor a
clearw6_lp
	push hl
	rept 6
		ld (hl),a
		inc l
	endm
	pop hl
	inc h
	djnz clearw6_lp
	ret

lning_frame
	ld (lning_frameno+1),a
	ld hl,lning_data
	ld de,0x5800
	ld b,24
lning_row
	push bc
		ld b,16
lning_char
		ld a,(hl)
lning_frameno
		sub 0
		jp c,lning_skip
		cp 0x10
		jp c,lning_pass
		cp 0x80
		jr z,lning_outline
		cp 0xc0
		jp nz,lning_skip
		ld a,0xff
lning_pass
		inc a
lning_pass0
		ld (de),a
lning_skip
		inc hl
		inc e
		djnz lning_char
	ld bc,0x0010
	ex de,hl
	add hl,bc
	ex de,hl
	pop bc
	djnz lning_row
	ret

lning_outline
	ld a,0x07 ; 0x38 ; frame colour
	jp lning_pass0

lning_data
	db 0x00, 0x00, 0x00, 0x10, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00
	db 0x00, 0x00, 0x00, 0x11, 0x12, 0x13, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00
	db 0x00, 0x00, 0x00, 0x00, 0x00, 0x14, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00
	db 0x8a, 0x89, 0x88, 0x87, 0x86, 0x85, 0x86, 0x87, 0x88, 0x89, 0x8a, 0x8b, 0x8c, 0x8d, 0x8e, 0x8f
	db 0x8b, 0xd6, 0xd0, 0xda, 0xd2, 0xd8, 0xd1, 0xdd, 0xd3, 0xd8, 0xdb, 0xd6, 0xd2, 0xd5, 0xdd, 0x90
	db 0x8c, 0xd8, 0xd2, 0xdc, 0xd4, 0xda, 0xd3, 0xdf, 0xd5, 0xda, 0xdd, 0xd8, 0xd4, 0xd7, 0xdf, 0x91
	db 0x8d, 0xda, 0xd4, 0xde, 0xd6, 0xdc, 0xd5, 0xe1, 0xd7, 0xdc, 0xdf, 0xda, 0xd6, 0xd9, 0xe1, 0x92
	db 0x8e, 0xdc, 0xd6, 0xe0, 0xd8, 0xde, 0xd7, 0xe3, 0xd9, 0xde, 0xe1, 0xdc, 0xd8, 0xdb, 0xe3, 0x93
	db 0x8f, 0xde, 0xd8, 0xe2, 0xda, 0xe0, 0xd9, 0xe5, 0xdb, 0xe0, 0xe3, 0xde, 0xda, 0xdd, 0xe5, 0x94
	db 0x90, 0xe0, 0xda, 0xe4, 0xdc, 0xe2, 0xdb, 0xe7, 0xdd, 0xe2, 0xe5, 0xe0, 0xdc, 0xdf, 0xe7, 0x95
	db 0x91, 0xe2, 0xdc, 0xe6, 0xde, 0xe4, 0xdd, 0xe9, 0xdf, 0xe4, 0xe7, 0xe2, 0xde, 0xe1, 0xe9, 0x96
	db 0x92, 0xe4, 0xde, 0xe8, 0xe0, 0xe6, 0xdf, 0xeb, 0xe1, 0xe6, 0xe9, 0xe4, 0xe0, 0xe3, 0xeb, 0x97
	db 0x93, 0xe6, 0xe0, 0xea, 0xe2, 0xe8, 0xe1, 0xed, 0xe3, 0xe8, 0xea, 0xe6, 0xe2, 0xe5, 0xed, 0x98
	db 0x94, 0xe8, 0xe2, 0xec, 0xe4, 0xea, 0xe3, 0xef, 0xe5, 0xea, 0xec, 0xe8, 0xe4, 0xe7, 0xef, 0x99
	db 0x95, 0x96, 0x97, 0x98, 0x99, 0x9a, 0x9b, 0x9c, 0x9d, 0x9e, 0x9f, 0x9e, 0x9d, 0x9c, 0x9b, 0x9a
	db 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x30, 0x00, 0x00, 0x00, 0x00, 0x00
	db 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x34, 0x33, 0x32, 0x31, 0x00, 0x00, 0x00, 0x00, 0x00
	db 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x35, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00
	db 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x36, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00
	db 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x37, 0x38, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00
	db 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x39, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00
	db 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x3a, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00
	db 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x3b, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00
	db 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x3c, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00
